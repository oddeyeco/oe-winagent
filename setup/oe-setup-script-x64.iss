; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OddEye Agent"
#define MyAppVersion "0.5"
#define MyAppPublisher "OddEye, Inc."
#define MyAppURL "https://www.oddeye.co/"

#define MyAppExeName "oe-tray-menu.exe"
#define MyAppServiceName "agent-service.exe"
#define MyAppTerminalName "oe-terminal.exe"
#define OddEyeDirName "OddEye Agent"
#define OddEyeDevDir "C:\Users\User\Documents\OE-Agent"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2BB3A430-E50D-48AA-890C-6988492633A7}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#OddEyeDevDir}\src\setup\LICENSE.TXT
OutputDir={#OddEyeDevDir}\setup\output\
OutputBaseFilename=oe-agent-setup-x64
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
AppMutex=oe_agent_sys_tray

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: desktopicon; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "Additional icons:"; 
Name: desktopicon\user; Description: "For the current user only"; GroupDescription: "Additional icons:";  Flags: exclusive 
Name: desktopicon\common; Description: "For all users"; GroupDescription: "Additional icons:"; Flags: exclusive unchecked

[Dirs]
Name: "{app}\scripts_available"
Name: "{app}\scripts_enabled"
Name: "{app}\perf_counters_available"; Flags: uninsalwaysuninstall

[Files]
Source: "{#OddEyeDevDir}\src\control\build-OESystemTrayController-Desktop_Qt_5_6_3_MSVC2013_64bit-Release\release\OEAgentControl.exe"; DestDir: "{app}"; Flags: ignoreversion;     DestName: {#MyAppExeName}
Source: "{#OddEyeDevDir}\src\control\build-OEConsolController-Desktop_Qt_5_6_3_MSVC2013_64bit-Release\release\OEConsolController.exe"; DestDir: "{app}"; Flags: ignoreversion; DestName: {#MyAppTerminalName}        
Source: "{#OddEyeDevDir}\src\build-OEAgentService-Desktop_Qt_5_6_3_MSVC2013_64bit-Release\release\OE-Agent.exe";                       DestDir: "{app}"; Flags: ignoreversion;                      DestName: {#MyAppServiceName}
         
Source: "{#OddEyeDevDir}\setup\bin64\platforms\*";    DestDir: "{app}\platforms"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#OddEyeDevDir}\setup\bin64\conf\*";         DestDir: "{app}\conf";      Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#OddEyeDevDir}\setup\bin32\scripts_available\*";       DestDir: "{app}\scripts_available"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#OddEyeDevDir}\setup\bin64\libeay32.dll";   DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\libssl32.dll";   DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\Qt5Core.dll";    DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\Qt5Gui.dll";     DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\Qt5Network.dll"; DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\Qt5Widgets.dll"; DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\ssleay32.dll";   DestDir: "{app}";           Flags: ignoreversion
Source: "{#OddEyeDevDir}\setup\bin64\vcredist_msvc2013_x64.exe"; DestDir: {tmp};  Flags: deleteafterinstall
;Source: "C:\Users\Aram\Documents\OE-Agent\setup\bin\vcruntime140.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\vcredist_msvc2013_x64.exe"; Check: VCRedistNeedsInstall
Filename: "{app}\{#MyAppServiceName}"; Parameters: -i; Flags: runhidden;
Filename: "{app}\{#MyAppServiceName}"; Flags: runhidden;
Filename: "{app}\{#MyAppTerminalName}"; Parameters: -dump; Flags: runhidden;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange("OddEye Controller in system tray", '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\conf\"; Description: "{#StringChange("Open configurations folder", '&', '&&')}"; Flags: shellexec runasoriginaluser nowait postinstall skipifsilent

[UninstallRun]
Filename: "{app}\{#MyAppTerminalName}"; Parameters: -stop; Flags: runhidden;
Filename: "{app}\{#MyAppServiceName}"; Parameters: -t; Flags: runhidden;
Filename: "{app}\{#MyAppServiceName}"; Parameters: -u; Flags: runhidden; 

[Registry]
;current user only
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "OddEye Agent Control"; ValueData: "{app}\{#MyAppExeName}"; Flags: uninsclearvalue
;Root: HKCU; Subkey: "Software\OddEyeAgent\"; ValueType: string; ValueName: "autostart"; ValueData: false; Flags: uninsdeletekey

[INI]
Filename: "{app}\conf\conf.ini"; Section: "TSDB"; Key: "uuid"; String: {code:GetSpecifiedUuid};


[Code]
#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF
type
  INSTALLSTATE = Longint;
const
  INSTALLSTATE_INVALIDARG = -2;  { An invalid parameter was passed to the function. }
  INSTALLSTATE_UNKNOWN = -1;     { The product is neither advertised or installed. }
  INSTALLSTATE_ADVERTISED = 1;   { The product is advertised but not installed. }
  INSTALLSTATE_ABSENT = 2;       { The product is installed for a different user. }
  INSTALLSTATE_DEFAULT = 5;      { The product is installed for the current user. }

  { Visual C++ 2013 Redistributable 12.0.21005 }
  VC_2013_REDIST_X86_MIN = '{13A4EE12-23EA-3371-91EE-EFB36DDFFF3E}';
  VC_2013_REDIST_X64_MIN = '{A749D8E6-B613-3BE3-8F5F-045C84EBA29B}';

  VC_2013_REDIST_X86_ADD = '{F8CFEB22-A2E7-3971-9EDA-4B11EDEFC185}';
  VC_2013_REDIST_X64_ADD = '{929FBD26-9020-399B-9A7A-751D61F0B942}';

function MsiQueryProductState(szProduct: string): INSTALLSTATE; 
  external 'MsiQueryProductState{#AW}@msi.dll stdcall';

function VCVersionInstalled(const ProductID: string): Boolean;
begin
  Result := MsiQueryProductState(ProductID) = INSTALLSTATE_DEFAULT;
end;

function VCRedistNeedsInstall: Boolean;
begin
  Result := not VCVersionInstalled(VC_2013_REDIST_X64_MIN);
end;

{-----------------------------------}

var
  MainPage: TWizardPage; 
  InstallHelpCheckBox: TNewCheckBox;  
  FolderToInstall: TNewEdit;

{ ================================================================== }
{ util method, equivalent to C# string.StartsWith }
function StartsWith(SubStr, S: String): Boolean;
begin
  Result:= Pos(SubStr, S) = 1;
end;

{ util method, equivalent to C# string.Replace }
function StringReplace(S, oldSubString, newSubString: String): String;
var
  stringCopy: String;
begin
  stringCopy := S; { Prevent modification to the original string }
  StringChange(stringCopy, oldSubString, newSubString);
  Result := stringCopy;
end;

function GetCommandlineParam(inParamName: String): String; 
var
   paramNameAndValue: String;
   i: Integer;
begin
   Result := '';

   for i := 0 to ParamCount do
   begin
     paramNameAndValue := ParamStr(i);
     if (StartsWith(inParamName, paramNameAndValue)) then
     begin
       Result := StringReplace(paramNameAndValue, inParamName + '=', '');
       break;
     end;
   end;
end;
{ ================================================================== }

procedure YourControlClick(Sender: TObject);
begin
  FolderToInstall.Enabled := Not FolderToInstall.Enabled;
  if FolderToInstall.Enabled = False Then 
    Wizardform.NextButton.Enabled := True
  else
    Wizardform.NextButton.Enabled := FolderToInstall.Text <> '';
end;

procedure MyEditChange(Sender: TObject);
begin
  // enable the next button if the edit box is not empty; disable otherwise
  WizardForm.NextButton.Enabled := FolderToInstall.Text <> '';
end;

procedure InitializeWizard;
var  
  LabelFolder: TLabel; 
    
begin
  Wizardform.NextButton.Enabled := False;

  MainPage := CreateCustomPage(wpSelectComponents  , 'Insert OddEye profile UUid', '(Optional)');
  LabelFolder := TLabel.Create(MainPage);
  {LabelFolder.Parent := WizardForm;}
  LabelFolder.Top := 20;
  LabelFolder.Left := 6;
  LabelFolder.Caption := 'UUID:'

  InstallHelpCheckBox := TNewCheckBox.Create(MainPage);
  InstallHelpCheckBox.Parent := MainPage.Surface;
  InstallHelpCheckBox.Top := LabelFolder.Top + LabelFolder.Height + 8;
  InstallHelpCheckBox.Left := LabelFolder.Left;
  InstallHelpCheckBox.Width := 380;
  InstallHelpCheckBox.Caption := 'Insert OddEye profile UUid';

  FolderToInstall := TNewEdit.Create(MainPage);
  FolderToInstall.Parent := MainPage.Surface;
  FolderToInstall.Top := InstallHelpCheckBox.Top + InstallHelpCheckBox.Height + 8;
  FolderToInstall.Left := InstallHelpCheckBox.Left;
  FolderToInstall.Width := InstallHelpCheckBox.Width;
  FolderToInstall.Enabled := False;
  FolderToInstall.OnChange := @MyEditChange;

  FolderToInstall.Text := GetCommandLineParam('/uuid');

  InstallHelpCheckBox.OnClick := @YourControlClick;
  InstallHelpCheckBox.Checked := True;

end;

procedure  CurPageChanged(CurPageID: Integer);
begin
  if CurPageID = MainPage.ID Then
  begin
     if  (Not WizardSilent) and (InstallHelpCheckBox.Checked = True) and (FolderToInstall.Text = '') then
      Wizardform.NextButton.Enabled := False
     else
      Wizardform.NextButton.Enabled := True;
  end;
    
end;

function GetSpecifiedUuid(Param: String  ): string;
var sUUidFromCmd : String;
begin
  Result := 'xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx';

  if WizardSilent then
  begin
    sUUidFromCmd := GetCommandLineParam('/uuid');
    if sUUidFromCmd <> '' then
      Result := sUUidFromCmd;
  end
  else if (InstallHelpCheckBox.Checked = true) and (FolderToInstall.Text <> '') then
    Result := FolderToInstall.Text
end;